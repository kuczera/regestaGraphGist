== GraphGists zum GraphReader
:author: Andreas Kuczera
:twitter: andreaskuczera
:tags: Graph Technologies, Digital Humanities, Medieval History
:neo4j-version: 3.5

=== Einführung

image::https://upload.wikimedia.org/wikipedia/commons/thumb/7/79/Hans_Burgkmair_d._%C3%84._%28zugeschr.%29_-_Bildnis_Kaiser_Friedrich_III.jpg/490px-Hans_Burgkmair_d._%C3%84._%28zugeschr.%29_-_Bildnis_Kaiser_Friedrich_III.jpg[]

In Form von CSV Dateien liegen Regesten zu Friedrich III vor und wurden auf Google Drive hochgeladen. Die CSV Dateien sind jeweils zwischen 5 und 12 MB groß und haben zwischen 8000 und 12000 Zeilen.

== vorbereitende Schritte

=== Konfiguration Neo4j

Um den Browser zu erlauben Guides von beliebigen Quellen zu laden, muss in `conf/neo4j.conf` folgende Zeile hinzugefügt werden:

[source,conf]
----
browser.remote_content_hostname_whitelist=*
----

Außerdem muss die APOC Bibliothek installiert sein (vgl. https://kuczera.github.io/Graphentechnologien/95_Anhang.html#die-apoc-bibliothek).

=== Datenbank bereinigen

Sollten bereits Daten in der lokalen Neo4j Instanz vorhanenden sein, bitte vorher alles löschen:

[source,cypher]
----
MATCH (n) DETACH DELETE n
----

== Daten importieren

WICHTIG: In den Einstellungen des neo4j-Browsers die Option 'Enable multiple Statement' auswählen.
image::https://raw.githubusercontent.com/kuczera/Graphreader/Bilder/enableMultipleStatement.png[Enable multiple Statements]

[source,cypher]
----
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1418-12-21", `name`:"Albert VI, Archduke of Austria", `personGnd`:"119532441", `personSex`:"m", `wikidataId`:"Q145606", `wikidataUrl`:"http://www.wikidata.org/entity/Q145606", `yearOfBirth`:"1418", `UNIQUE IMPORT ID`:542});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1402-02-10", `name`:"Eleanor of Aragon", `personGnd`:"136924344", `personSex`:"w", `wikidataId`:"Q241245", `wikidataUrl`:"http://www.wikidata.org/entity/Q241245", `yearOfBirth`:"1402", `UNIQUE IMPORT ID`:874});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1500-06-13", `name`:"Ernest of Bavaria", `personGnd`:"119004615", `personSex`:"m", `wikidataId`:"Q541271", `wikidataUrl`:"http://www.wikidata.org/entity/Q541271", `yearOfBirth`:"1500", `UNIQUE IMPORT ID`:1369});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1480-01-10", `name`:"Margaret of Austria, Duchess of Savoy", `personGnd`:"118730983", `personSex`:"w", `wikidataId`:"Q157100", `wikidataUrl`:"http://www.wikidata.org/entity/Q157100", `yearOfBirth`:"1480", `UNIQUE IMPORT ID`:6664});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1352-01-01", `name`:"Viridis Visconti", `personGnd`:"13035936X", `personSex`:"w", `wikidataId`:"Q271827", `wikidataUrl`:"http://www.wikidata.org/entity/Q271827", `yearOfBirth`:"1352", `UNIQUE IMPORT ID`:6822});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1447-12-24", `name`:"Albert IV, Duke of Bavaria", `personGnd`:"118644327", `personSex`:"m", `wikidataId`:"Q60942", `wikidataUrl`:"http://www.wikidata.org/entity/Q60942", `yearOfBirth`:"1447", `UNIQUE IMPORT ID`:12223});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1472-04-14", `name`:"Bianca Maria Sforza", `personGnd`:"118885294", `personSex`:"w", `wikidataId`:"Q161136", `wikidataUrl`:"http://www.wikidata.org/entity/Q161136", `yearOfBirth`:"1472", `UNIQUE IMPORT ID`:12584});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1415-09-30", `name`:"Frederick III, Holy Roman Emperor", `personGnd`:"118535773", `personSex`:"m", `wikidataId`:"Q150966", `wikidataUrl`:"http://www.wikidata.org/entity/Q150966", `yearOfBirth`:"1415", `UNIQUE IMPORT ID`:18621});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1351-11-09", `name`:"Leopold III, Duke of Austria", `personGnd`:"104089512", `personSex`:"m", `wikidataId`:"Q275651", `wikidataUrl`:"http://www.wikidata.org/entity/Q275651", `yearOfBirth`:"1351", `UNIQUE IMPORT ID`:18636});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1391-11-07", `name`:"Edward", `personGnd`:"100969224", `personSex`:"m", `wikidataId`:"Q294607", `wikidataUrl`:"http://www.wikidata.org/entity/Q294607", `yearOfBirth`:"1391", `UNIQUE IMPORT ID`:22631});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1493-11-22", `name`:"William IV, Duke of Bavaria", `personGnd`:"118632868", `personSex`:"m", `wikidataId`:"Q60936", `wikidataUrl`:"http://www.wikidata.org/entity/Q60936", `yearOfBirth`:"1493", `UNIQUE IMPORT ID`:23937});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1416-01-01", `name`:"Margaret of Austria", `personGnd`:"139150129", `personSex`:"w", `wikidataId`:"Q79176", `wikidataUrl`:"http://www.wikidata.org/entity/Q79176", `yearOfBirth`:"1416", `UNIQUE IMPORT ID`:24024});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1495-09-23", `name`:"Louis X, Duke of Bavaria", `personGnd`:"11872939X", `personSex`:"m", `wikidataId`:"Q61496", `wikidataUrl`:"http://www.wikidata.org/entity/Q61496", `yearOfBirth`:"1495", `UNIQUE IMPORT ID`:24036});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1459-03-31", `name`:"Maximilian I", `personGnd`:"118579371", `personSex`:"m", `wikidataId`:"Q150726", `wikidataUrl`:"http://www.wikidata.org/entity/Q150726", `yearOfBirth`:"1459", `UNIQUE IMPORT ID`:24214});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1457-02-22", `name`:"Mary of Burgundy", `personGnd`:"11857776X", `personSex`:"w", `wikidataId`:"Q157073", `wikidataUrl`:"http://www.wikidata.org/entity/Q157073", `yearOfBirth`:"1457", `UNIQUE IMPORT ID`:24346});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1434-09-27", `name`:"Eleanor of Portugal, Holy Roman Empress", `personGnd`:"119367459", `personSex`:"w", `wikidataId`:"Q161149", `wikidataUrl`:"http://www.wikidata.org/entity/Q161149", `yearOfBirth`:"1434", `UNIQUE IMPORT ID`:24366});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1424-01-01", `name`:"Catherine of Austria", `personGnd`:"129038342", `personSex`:"w", `wikidataId`:"Q114431", `wikidataUrl`:"http://www.wikidata.org/entity/Q114431", `yearOfBirth`:"1424", `UNIQUE IMPORT ID`:24428});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1465-03-25", `name`:"Kunigunde of Austria", `personGnd`:"122504852", `personSex`:"w", `wikidataId`:"Q237712", `wikidataUrl`:"http://www.wikidata.org/entity/Q237712", `yearOfBirth`:"1465", `UNIQUE IMPORT ID`:24785});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1476-01-01", `name`:"Anne of Brittany", `personGnd`:"119020688", `personSex`:"w", `wikidataId`:"Q201143", `wikidataUrl`:"http://www.wikidata.org/entity/Q201143", `yearOfBirth`:"1476", `UNIQUE IMPORT ID`:30031});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1502-04-22", `name`:"Susanna of Bavaria", `personGnd`:"12452169X", `personSex`:"w", `wikidataId`:"Q71788", `wikidataUrl`:"http://www.wikidata.org/entity/Q71788", `yearOfBirth`:"1502", `UNIQUE IMPORT ID`:30064});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1492-05-12", `name`:"Sabina of Bavaria", `personGnd`:"132802430", `personSex`:"w", `wikidataId`:"Q69427", `wikidataUrl`:"http://www.wikidata.org/entity/Q69427", `yearOfBirth`:"1492", `UNIQUE IMPORT ID`:30117});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1377-01-01", `name`:"Ernest, Duke of Austria", `personGnd`:"128967943", `personSex`:"m", `wikidataId`:"Q276526", `wikidataUrl`:"http://www.wikidata.org/entity/Q276526", `yearOfBirth`:"1377", `UNIQUE IMPORT ID`:30252});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1394-01-01", `name`:"Cymburgis of Masovia", `personGnd`:"136913105", `personSex`:"w", `wikidataId`:"Q112077", `wikidataUrl`:"http://www.wikidata.org/entity/Q112077", `yearOfBirth`:"1394", `UNIQUE IMPORT ID`:34066});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1478-07-02", `name`:"Philip I of Castile", `personGnd`:"11864162X", `personSex`:"m", `wikidataId`:"Q157098", `wikidataUrl`:"http://www.wikidata.org/entity/Q157098", `yearOfBirth`:"1478", `UNIQUE IMPORT ID`:35892});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1504-01-01", `name`:"George of Austria", `personGnd`:"119684055", `personSex`:"m", `wikidataId`:"Q552986", `wikidataUrl`:"http://www.wikidata.org/entity/Q552986", `yearOfBirth`:"1504", `UNIQUE IMPORT ID`:36987});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1488-05-10", `name`:"Sidonie of Bavaria", `personGnd`:"1036755878", `personSex`:"w", `wikidataId`:"Q100474", `wikidataUrl`:"http://www.wikidata.org/entity/Q100474", `yearOfBirth`:"1488", `UNIQUE IMPORT ID`:41449});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`dateOfBirth`:"1489-01-01", `name`:"Sibylle of Bavaria", `personGnd`:"136863116", `personSex`:"w", `wikidataId`:"Q97171", `wikidataUrl`:"http://www.wikidata.org/entity/Q97171", `yearOfBirth`:"1489", `UNIQUE IMPORT ID`:41893});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`wikidataId`:"Q922141", `wikidataUrl`:"http://www.wikidata.org/entity/Q922141", `UNIQUE IMPORT ID`:68091});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`wikidataId`:"Q2376894", `wikidataUrl`:"http://www.wikidata.org/entity/Q2376894", `UNIQUE IMPORT ID`:68092});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`wikidataId`:"Q13407861", `wikidataUrl`:"http://www.wikidata.org/entity/Q13407861", `UNIQUE IMPORT ID`:68093});
CREATE (:`wikidataPerson`:`UNIQUE IMPORT LABEL` {`wikidataId`:"Q56720527", `wikidataUrl`:"http://www.wikidata.org/entity/Q56720527", `UNIQUE IMPORT ID`:74602});
CREATE INDEX ON :`wikidataPerson`(`dateOfBirth`);
CREATE INDEX ON :`wikidataPerson`(`wikidataUrl`);
CREATE INDEX ON :`wikidataPerson`(`yearOfBirth`);
CREATE CONSTRAINT ON (node:`UNIQUE IMPORT LABEL`) ASSERT (node.`UNIQUE IMPORT ID`) IS UNIQUE;
CALL db.awaitIndex(':`wikidataPerson`(`yearOfBirth`)');
CALL db.awaitIndex(':`wikidataPerson`(`wikidataUrl`)');
CALL db.awaitIndex(':`wikidataPerson`(`dateOfBirth`)');
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24366}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:874}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:6822}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:18621}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:18621}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:18636}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24366}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:22631}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:35892}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:6664}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:36987}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:68091}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:68092}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:68093}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24366}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24366}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:23937}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24036}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30117}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30064}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:41893}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:41449}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:1369}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24024}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24428}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:542}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:18621}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24024}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:34066}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24428}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:34066}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:542}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:34066}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:18621}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:34066}) CREATE (n1)-[r:`IS_CHILD_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:12223}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:12584}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24366}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:18621}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24346}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:12584}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30031}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24346}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:18621}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24366}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:12223}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24785}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:24214}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30031}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:34066}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:74602}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:34066}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:30252}), (n2:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:74602}) CREATE (n1)-[r:`SPOUSE_OF`]->(n2);
MATCH (n:`UNIQUE IMPORT LABEL`)  WITH n LIMIT 20000 REMOVE n:`UNIQUE IMPORT LABEL` REMOVE n.`UNIQUE IMPORT ID`;
DROP CONSTRAINT ON (node:`UNIQUE IMPORT LABEL`) ASSERT (node.`UNIQUE IMPORT ID`) IS UNIQUE;
----

== Graphdatenmodell

image::https://raw.githubusercontent.com/sarmbruster/akdwmainz_fiii_guide/master/img/graphmodel.png[Graphdatenmodell]

== Import Regesten

Nachdem die Datenbank nun vorbereitet ist werden wir nun die CSV Daten importieren.
Die Struktur der CSV Datei für die Regesten kann man untersuchen:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc/export?format=csv&id=1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc&gid=1917262438" AS line
RETURN line LIMIT 5
----


[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc/export?format=csv&id=1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc&gid=1917262438" AS line
CREATE (r:Regest {regid:line.persistent_identifier, Text:line.summary, Überlieferung:line.archival_history,ident:line.identifier})
MERGE (d:Datum {startdate:line.start_date, enddate:line.end_date})
MERGE (o:Ort {ort:line.name, latitude:toFloat(line.latitude), longitude:toFloat(line.longitude)})
CREATE (r)-[:HAT_DATUM]->(d)
CREATE (r)-[:HAT_ORT]->(o);
----

== Import Lemmata/Herrscherhandeln)

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/1yVrW_rGWoEZ7jtJTtdVUzD9WhCq5ZTUI1HLV_UGAM3U/export?format=csv&id=1yVrW_rGWoEZ7jtJTtdVUzD9WhCq5ZTUI1HLV_UGAM3U&gid=305253904"
AS line FIELDTERMINATOR ','
MATCH (r:Regest{ident:line.regid})
MERGE (l:Lemma{lemma:line.lemma})
MERGE (r)-[:HERRSCHERHANDELN]->(l);
----

== Geo Abfragen und Neo4j Spatial

=== mit Cypher Bordmitteln

Cypher hat mit `point()` und `distance()` zwei (noch undokumentierte) Funktionen, die ohne das Spatial plugin bereits hilfreich sind. Wir wollen z.B. wissen wie weit Dinkelsbühl und Augsburg entfernt sind:

[source,cypher]
----
MATCH (o1:Ort{ort:'Dinkelsbühl'}), (o2:Ort{ort:'Augsburg'})
RETURN o1.ort, o2.ort, distance(point(o1),point(o2)) AS distance
----

=== mit Neo4j Spatial

https://github.com/neo4j-contrib/spatial[Neo4j Spatial] ist eine Erweiterung von Neo4j, die mit R-Bäumen eine Vielzahl von Geo-Abfragen erlaubt. z.B. Knoten in bestimmter Entfernung, Polygonschnitte und vieles mehr.

Zuerst muss ein Layer angelegt werden:

[source,cypher]
----
CALL spatial.addPointLayer("geom")
----

Alle `Ort` Knoten müssen explizit zum Layer hinzugefügt werden:

[source,cypher]
----
MATCH (o:Ort)
CALL spatial.addNode("geom",o) YIELD node
RETURN node
----


Umkreissuche um bekannte Koordinaten:

[source,cypher]
----
CALL spatial.withinDistance('geom',{longitude:10.35,latitude:49.06},100) YIELD node, distance
RETURN node.ort, node.longitude, node.latitude, distance
----

[source,cypher]
----
MATCH(o:Ort{ort:'Dinkelsbühl'})
CALL spatial.withinDistance('geom',o,100) YIELD node, distance
RETURN node.ort, node.longitude, node.latitude, distance
----


== Itinerar

Vorab verbinden wir die `Datum` Knoten untereinander zu einer verketteten Liste entlang der Zeitachse:

[source,cypher]
----
MATCH (d:Datum)
WITH apoc.coll.sortNodes(collect(d), "startdate") AS dates
UNWIND apoc.coll.pairsMin(dates) AS pair
WITH pair[0] AS start, pair[1] AS end
CREATE (start)-[:NAECHSTES_DATUM]->(end)
----

Mit dem "Zeitpfeil" kann man nun den Itinerar abfragen:

[source,cypher]
----
MATCH dates=(s:Datum)-[:NAECHSTES_DATUM*]->(e:Datum)
WHERE not ( (s)<-[:NAECHSTES_DATUM]-() ) and not ( (e)-[:NAECHSTES_DATUM]->() )
UNWIND nodes(dates) AS d
MATCH (d)<-[:HAT_DATUM]-()-[:HAT_ORT]->(o:Ort)
WITH collect( [d,o]) AS route
UNWIND range(0, size(route)-2) AS x
WITH route[x][0].enddate as left, route[x][1].ort AS from, route[x+1][0].startdate AS arrived, route[x+1][1].ort AS to, distance(point(route[x][1]), point(route[x+1][1]))/1000.0 AS distance
WHERE distance > 0
RETURN from, left, to, arrived, distance
----

== Itinerar in Google Maps

https://www.google.de/maps[Google Maps] kann unter "My Maps" eigene Dateien im kml Format hochgeladen werden. Mit einem kurzen Programm - hier in Groovy geschrieben - holt man sich aus dem Graphen die Informationen und erzeugt einen kml-Datei

[source,groovy]
----
#!/usr/bin/env groovy
@Grab('org.neo4j.driver:neo4j-java-driver:1.1.0')

import org.neo4j.driver.v1.Driver
import org.neo4j.driver.v1.GraphDatabase
import org.neo4j.driver.v1.Session
import org.neo4j.driver.v1.StatementResult
import org.neo4j.driver.v1.Record
import groovy.xml.*

def xml = new StreamingMarkupBuilder(encoding: "utf-8").bind {
  mkp.xmlDeclaration(version: "1.0", encoding: "utf-8")

  Driver driver = GraphDatabase.driver("bolt://localhost")
  Session session = null
  try {
    session = driver.session()
    StatementResult rs = session.run( """match dates=(s:Datum)-[:NAECHSTES_DATUM*]->(e:Datum)
  where not ( (s)<-[:NAECHSTES_DATUM]-() )  and not ( (e)-[:NAECHSTES_DATUM]->() )
  unwind nodes(dates) as d
  match (d)<-[:HAT_DATUM]-()-[:HAT_ORT]->(o:Ort)
  return d.startdate as startdate, d.enddate as enddate, o.ort as ort, o.latitude as lat, o.longitude as lon""")

    Document {

      def previousOrt = null
      def travelCoordinates=""
        rs.each { record ->
          def ort = record.get("ort").asString()
          if (previousOrt != ort ) {
            previousOrt = ort
            def coordString = "${record.get('lon').asDouble()},${record.get('lat').asDouble()},0"
            def startdate = record.get("startdate").asString()
            def enddate = record.get("enddate").asString()
            Placemark {
              name ort
              description "von $startdate bis $enddate"
              Point {
                coordinates coordString
              }
              TimeSpan {
                begin startdate
                end enddate
              }
            }
            travelCoordinates += coordString + ","
          }
        }
        Placemark {
          name "Itinerar"
          LineString {
            coordinates travelCoordinates[0..-2] //strip last comma
          }
        }
    }
  } finally {
    session?.close()
  }

}

println XmlUtil.serialize(xml)
----

== Import von eindeutigen Erschließungspunkten

Im Feld `archival_history` befinden sich Texte, die URL Referenzen enthalten. Diese wollen wir extrahieren und als Knoten speichern. So kann nachvollzogen werden welche dieser URLs von welchen Regesten referenziert wird:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc/export?format=csv&id=1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc&gid=1917262438" AS line
RETURN line.archival_history LIMIT 5
----

Um die URL des Links und dessen Text zu exrahieren, müssen wir mit regular expressions arbeiten. Dies wird in APOC unterstützt:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc/export?format=csv&id=1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc&gid=1917262438" AS line
RETURN apoc.text.regexGroups(line.archival_history, "<link (\\S+)>(\\S+)</link>") LIMIT 10
----

Damit kann nun importiert werden:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc/export?format=csv&id=1qw4VGhi5RTg7b8KhGXpXR8tdhYsjEIdYkKucl-YlNJc&gid=1917262438" AS line
WITH line
WHERE line.archival_history CONTAINS "link"
MATCH (reg:Regest {regid:line.persistent_identifier})
UNWIND apoc.text.regexGroups(line.archival_history, "<link (\\S+)>(\\S+)</link>") as link
MERGE (ref:Referenz {url:link[1]}) ON CREATE SET ref.title=link[2]
MERGE (reg)-[:REFERENZIERT]->(ref)
----
